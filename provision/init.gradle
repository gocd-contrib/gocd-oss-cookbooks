//
// This file is managed by chef. Any changes will be lost.
//

import org.gradle.util.GradleVersion

addListener new BuildAdapter() {
  def void projectsLoaded(Gradle gradle) {

    // As soon as the projects are loaded, we add our repository before any other repositories
    gradle.rootProject {
      buildscript.repositories {
        maven { url "https://maven-mirrors.gocd.org/repository/internal" }
      }
      repositories {
        maven { url "https://maven-mirrors.gocd.org/repository/internal" }
      }
    }
    gradle.rootProject.allprojects {
      buildscript.repositories {
        maven { url "https://maven-mirrors.gocd.org/repository/internal" }
      }
      repositories {
        maven { url "https://maven-mirrors.gocd.org/repository/internal" }
      }
    }
  }
}

// if a build cache url is specified, use it
if (GradleVersion.current() >= GradleVersion.version("3.5") && System.getenv().containsKey('GOCD_BUILD_CACHE_URL')) {
  gradle.settingsEvaluated { settings ->
    settings.buildCache {

      remote(org.gradle.caching.http.HttpBuildCache) {
        url = System.getenv().get('GOCD_BUILD_CACHE_URL')
        push = true
      }
    }
  }
}
